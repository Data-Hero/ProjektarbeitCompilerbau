/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options { STATIC = false; MULTI=true; VISITOR=true; NODE_EXTENDS="BaseNode";}

PARSER_BEGIN(NewAwkParser)
package de.fhdo.fsc.project;

import java.io.FileReader;

public class NewAwkParser {
    public static void main(String[] args) {
        try {
                NewAwkParser parser = new NewAwkParser(new FileReader("./src/main/test/test1.na"));
                SimpleNode node = parser.Start();
                node.dump("");
            } catch (Exception e) { System.out.println(e); }
    }
}
PARSER_END(NewAwkParser)

SKIP : {
    " "
|   "\t"
|   "\n"
|   "\r"
|   < "//" (~["\n","\r"])* ("\n" | "\r" | "\r\n") > // Skip single line comments
}

// Keywords
TOKEN :
{
    < INT: "int" >
|   < DOUBLE : "double">
|   < FLOAT : "float">
|   < CHAR : "char">
|   < BOOLEAN : "boolean">
|   < STRING : "string" >
|   < TYPE : "type">
|   < VOID : "void" >
|   < NULL: "null">
|   < SMARTSWITCH : "@">
|   < RETURN: "return">
|   < ENUM_SEPERATOR: ",">
|   < EXPRESSION_TERMINATOR: ";">
|   < IF: "if">
|   < ELSE: "else">
|   < WHILE: "while">
|   < FOREACH: "foreach">
|   < IN: "in">
}

// Datatypes
TOKEN :
{
    < #LETTER: ["A"-"Z","_","a"-"z"]>
|   < #DIGIT: ["0"-"9"]>
}

// Date
TOKEN :
{
    < DATE: < TWO_DIGIT >"-"< TWO_DIGIT>"-"< TWO_DIGIT ><TWO_DIGIT>>
|   < TWO_DIGIT: (["0"-"9"]) {2} >
|   < AMPM: "am"|"pm" >
|   < TIME: <TWO_DIGIT>":"<TWO_DIGIT> >
}

// Selector classes
TOKEN :
{
    < PUNCT: "punct" >
|   < GRAPH: "graph"> // Letters, numbers and punctuation
|   < LOWER: "lower"> // Lowercase Letters
|   < ALPHA: "alpha"> // Letters
|   < ALNUM: "alnum"> // Alphanumerics
|   < PRINT: "print"> // Letters, numb ers, punctuation and whitespace
|   < CNTRL: "cntrl"> // Control characters
|   < SPACE: "space"> // Space characters
|   < BLANK: "blank"> // Space and tab
|   < DIGITS: "digit">// Digits
}

// Literals
TOKEN :
{
    <INTEGER_LITERAL: <DECIMAL_LITERAL>>
|   <#DECIMAL_LITERAL: ("+" | "-")? ["1"-"9"] (["0"-"9"])*>
|   <FLOATING_POINT_LITERAL: ("+" | "-")? (["0"-"9"])+ "." (["0"-"9"])*>
|   <CHARACTER_LITERAL: "\'" (~["\'","\\","\n","\r"] | "\\" (["n","t","b","r","f","\\","\'","\""] | ["0"-"7"] (["0"-"7"])? | ["0"-"3"] ["0"-"7"] ["0"-"7"])) "\'">
|   <STRING_LITERAL: "\"" ( ~["\"","\\","\n","\r"] | "\\" ( ["n","t","b","r","f","\\","\'","\""] | ["0"-"7"] (["0"-"7"])?  | ["0"-"3"] ["0"-"7"] ["0"-"7"] | ( ["\n","\r"] | "\r\n")))* "\"">
|   <TYPE_LITERAL: ">" ( ~["<",">","\\","\n","\r"] | "\\" ( [">", "<", "n","t","b","r","f","\\"] | ["0"-"7"] (["0"-"7"])?  | ["0"-"3"] ["0"-"7"] ["0"-"7"] | ( ["\n","\r"] | "\r\n")))* "<">
}

// Built-in Functions
TOKEN :
{
    < CONTERT_FUNCTION: ( "toBoolean" | "toCharacter" | "toDouble" | "toInteger" | "toString" )>
|   < CHECKTYPE_FUNCTION: ( "isBoolean" | "isCharacter" | "isDouble" | "isInteger" | "isString" )>
|   < LENGTH_FUNCTION: ( "length" )>
}

// Identifier
TOKEN :
{
    < IDENTIFIER: <LETTER> (<LETTER> | <DIGIT>)*>
}

void DateTime() : {}{<DATE><TIME><AMPM>}

ASTStart Start():{} {
    (LOOKAHEAD(3) FunctionDefinition())*
    (Element())+
    <EOF>
    { return jjtThis; }
}

// Base element
void Element():{} {
    Block()
|   (Expression() <EXPRESSION_TERMINATOR>)
}

// Block elements
void Block():{} {
    WhileLoopDefinition()
|   ForeachLoopDefinition()
|   IfDefinition()
}

// Expressions
void Expression():{} {
    AssingmentExpression()
}
/**
*     < INT: "int" >
  |   < DOUBLE : "double">
  |   < FLOAT : "float">
  |   < CHAR : "char">
  |   < BOOLEAN : "boolean">
  |   < STRING : "string" >
  |   < TYPE : "type">
  |   < VOID : "void" >
*/

void AssingmentExpression():{} {
    VariableDefinitionExpression() ("=" LogicalExpression() { jjtThis.data.put("value", token.image); })*
}

void VariableDefinitionExpression():{} {
    [
        LOOKAHEAD(Datatype() Identifier())
        Datatype() { jjtThis.data.put("type", token.image); }
    ]
    Identifier() { jjtThis.data.put("name", token.image); }
}

void LogicalExpression():{} {
    CompareExpression() [("&&" | "||") LogicalExpression()]
}

void CompareExpression():{} {
    AdditiveExpression() [("==" | "!=" | "<=" | ">=" | "<" | ">") CompareExpression()]
}

void AdditiveExpression():{} {
    MultiplicativeExpression() [("+" | "-") AdditiveExpression()]
}

void MultiplicativeExpression():{} {
    PrefixExpression() [("*" | "/") MultiplicativeExpression()]
}

void PowerExpression():{} {
    PrefixExpression() [("**" | "//") PowerExpression() ]
}

void ModuloExpression():{} {
    PrefixExpression() ["%" PowerExpression() ]
}

void PrefixExpression():{} {
    ["!"] UnaryExpression() [SmartSwitchDefinition()] // ToDo: Move SmartSwitchDefinition() to a better position?
}

void UnaryExpression():{} {
    LOOKAHEAD(2) "(" LogicalExpression() ")"
    | LOOKAHEAD(2) FunctionCallExpression()
    | IsDatatype()
    | ToDatatype()
    | LengthDatatype()
    | ValueExpression()
}

void ValueExpression():{} {
    (<STRING_LITERAL>
    | <CHARACTER_LITERAL>
    | <TYPE_LITERAL>
    | <INTEGER_LITERAL>
    | <FLOATING_POINT_LITERAL>
    | <NULL>
    | Identifier()) { jjtThis.data.put("value", token.image); }
}

void FunctionCallExpression():{} {
    Identifier() "(" [LogicalExpression() (<ENUM_SEPERATOR> LogicalExpression())*] ")"
}

void StringExpression():{} {
    <STRING_LITERAL>
    | Identifier()
    | <CHARACTER_LITERAL>
    | <INTEGER_LITERAL>
    | <FLOATING_POINT_LITERAL>
    | <NULL>
}

void Identifier():{} {
    <IDENTIFIER>
    {
        jjtThis.data.put("name", token.image);
    }
}

// Function
void FunctionReturnExpression():{} {
    <RETURN> [LogicalExpression()] <EXPRESSION_TERMINATOR>
}

void FunctionBodyDefinition():{} {
    "{" (Element())* (FunctionReturnExpression())? "}"
}

void FunctionHeaderDefinition():{} {
    ReturnDatatype() Identifier() "(" (LOOKAHEAD(2) Datatype() Identifier())? (LOOKAHEAD(3)<ENUM_SEPERATOR> Datatype() Identifier())* ")"
}

void FunctionDefinition():{} {
    FunctionHeaderDefinition() FunctionBodyDefinition()
}

void ForeachLoopDefinition():{} {
    <FOREACH> "(" Identifier() <IN> UnaryExpression() ")" FunctionBodyDefinition()
}

void WhileLoopDefinition():{} {
    <WHILE> "(" LogicalExpression() ")" FunctionBodyDefinition()
}

void IfDefinition():{} {
    <IF> "(" LogicalExpression()  ")" FunctionBodyDefinition()
    (LOOKAHEAD(2) <ELSE> <IF> "(" LogicalExpression() ")" FunctionBodyDefinition())*
    (<ELSE> FunctionBodyDefinition())?
}

// Smart Switch
void SmartSwitchSelektor():{} {
    <PUNCT> | <GRAPH> | <LOWER> | <ALPHA> | <ALNUM> | <PRINT> | <CNTRL> | <SPACE> | <BLANK> | <DIGITS>
}


void SmartSwitchConditionDefinition():{} {
    ("!" ":" SmartSwitchSelektor() ":") | (":" SmartSwitchSelektor() ":") // 2 functions or OR seem like the only options. What about >>["!"] ":" SmartSwitchSelektor() ":"<< ?
}

void SmartSwitchCaseDefinition():{} {
    SmartSwitchConditionDefinition() FunctionBodyDefinition()
}

void SmartSwitchDefinition():{} {
    <SMARTSWITCH> "{" SmartSwitchCaseDefinition() "}"
}


// Integrated Funtions
void IsDatatype():{} {
     <CHECKTYPE_FUNCTION> "(" UnaryExpression() ")"
}

void ToDatatype():{} {
    <CONTERT_FUNCTION> "(" UnaryExpression() ")"
}

void LengthDatatype():{} {
    <LENGTH_FUNCTION> "(" UnaryExpression() ")"
}

// Datatype
void BasicDatatype():{} {
    <INT> | <DOUBLE> | <CHAR> | <BOOLEAN> | <STRING> | <TYPE> | <IDENTIFIER>
}


void Datatype():{} {
    BasicDatatype() ("[]")*
}

void ReturnDatatype():{} {
    Datatype() | <VOID>
}